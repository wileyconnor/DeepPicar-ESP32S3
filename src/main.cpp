#include "freertos/FreeRTOS.h"
#include "freertos/task.h"
#include "esp_timer.h"
#include "esp_system.h"
#include <stdio.h>

#include "esp_camera.h"
#include <WiFi.h>
#include "NeuralNetwork.h"

#include "camera_pins.h"
#include "control.h" // motor control
#include "camera_lock.h"

SemaphoreHandle_t camera_mux = NULL;
// prepare input image tensor

#define USE_INT8 1

#define SETUP_AP 1   // 1: setup AP mode, 0: setup Station mode
#define WAIT_SERIAL 1 // 1: wait for serial monitor, 0: don't wait

#if SETUP_AP==1
const char* ssid = "ESP32S3";
const char* password = "123456789"; 
#else
const char* ssid = "robocar";
const char* password = "robocar1234";
#endif

// enable deeppicar dnn by default
int g_use_dnn = 0; // set by web server

// DNN model pointer
NeuralNetwork *g_nn;

void startCameraServer();
void dnn_loop(camera_fb_t *fb);

void setup() {
  // Serial init
  Serial.begin(115200);
#if WAIT_SERIAL==1
  while(!Serial) {
    static int retries = 0;
    delay(1000); // Wait for serial monitor to open
    if (retries++ > 5) {
      break;
    }
  } // When the serial monitor is turned on, the program starts to execute
#endif
  Serial.setDebugOutput(false);
  Serial.println();

  // WiFi init
#if SETUP_AP==1
  Serial.print("Setting AP (Access Point)");
  WiFi.setTxPower(WIFI_POWER_19_5dBm);
  WiFi.softAP(ssid, password);
  Serial.print("Use 'http://");
  Serial.print(WiFi.softAPIP());
  Serial.println("' to connect");
#else
  Serial.print("Connecting to WiFi");
  WiFi.mode(WIFI_STA);
  // WiFi.setTxPower(WIFI_POWER_19_5dBm);
  // WiFi.setMinSecurity(WIFI_AUTH_WPA_PSK);
  WiFi.begin(ssid, password);
  WiFi.setSleep(false);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.println("WiFi connected");
  Serial.print("Use 'http://");
  Serial.print(WiFi.localIP());
  Serial.println("' to connect");
#endif
  // printf("wifi_task_core_id: %d\n", CONFIG_ESP32_WIFI_TASK_CORE_ID);

  // camera init
  camera_config_t config;
  config.ledc_channel = LEDC_CHANNEL_0;
  config.ledc_timer = LEDC_TIMER_0;
  config.pin_d0 = Y2_GPIO_NUM;
  config.pin_d1 = Y3_GPIO_NUM;
  config.pin_d2 = Y4_GPIO_NUM;
  config.pin_d3 = Y5_GPIO_NUM;
  config.pin_d4 = Y6_GPIO_NUM;
  config.pin_d5 = Y7_GPIO_NUM;
  config.pin_d6 = Y8_GPIO_NUM;
  config.pin_d7 = Y9_GPIO_NUM;
  config.pin_xclk = XCLK_GPIO_NUM;
  config.pin_pclk = PCLK_GPIO_NUM;
  config.pin_vsync = VSYNC_GPIO_NUM;
  config.pin_href = HREF_GPIO_NUM;
  config.pin_sccb_sda = SIOD_GPIO_NUM;
  config.pin_sccb_scl = SIOC_GPIO_NUM;
  config.pin_pwdn = PWDN_GPIO_NUM;
  config.pin_reset = RESET_GPIO_NUM;
  config.xclk_freq_hz = 20000000;
  if (!g_use_dnn) {
    config.frame_size = FRAMESIZE_QVGA;
    config.pixel_format = PIXFORMAT_JPEG; // for streaming
  } else {
    config.frame_size = FRAMESIZE_QQVGA;
    config.pixel_format = PIXFORMAT_RGB565; // for dnn
  }
  config.grab_mode = CAMERA_GRAB_LATEST;
  config.fb_location = CAMERA_FB_IN_PSRAM;
  config.jpeg_quality = 12;
  config.fb_count = 2;

  // camera init
  esp_err_t err = esp_camera_init(&config);
  if (err != ESP_OK) {
    Serial.printf("Camera init failed with error 0x%x", err);
    return;
  } 

  Serial.println("Camera init success!");
  Serial.printf("Camera info: framesize=%d, pixel_format=%d\n", config.frame_size, config.pixel_format);
  
  // create camera access mutex to serialize esp_camera_fb_get()/esp_camera_fb_return()
  camera_mux = xSemaphoreCreateMutex();
  if (camera_mux == NULL) {
    Serial.println("Failed to create camera mutex");
  }

  // start stream and command http servers
  startCameraServer();

  // setup motor control
  setup_control();

  // register pilotnet algo
  g_nn = new NeuralNetwork();

  // end of init
  Serial.println("Ready");
}

// loopTask Core1, prio=1 stack=4096
void loop() {
  if (g_use_dnn) {
    // run dnn control loop
    camera_fb_t *fb = NULL;
    if (camera_mux) xSemaphoreTake(camera_mux, portMAX_DELAY);
    fb = esp_camera_fb_get();
    // dnn control loop
    dnn_loop(fb);
    // return camera frame buffer
    esp_camera_fb_return(fb);
    if (camera_mux)  xSemaphoreGive(camera_mux);
  } else {
    // manual control
    delay(1000);
    Serial.print(".");
  }
}

#define DEBUG_TFLITE 0

#if DEBUG_TFLITE==1
#include "img.h"  // Use a static image for debugging
#endif


#include "NeuralNetwork.h"
extern NeuralNetwork *nn;


uint32_t rgb565torgb888(uint16_t color)
{
    uint8_t hb, lb;
    uint32_t r, g, b;

    lb = (color >> 8) & 0xFF;
    hb = color & 0xFF;

    r = (lb & 0x1F) << 3;
    g = ((hb & 0x07) << 5) | ((lb & 0xE0) >> 3);
    b = (hb & 0xF8);
    
    return (r << 16) | (g << 8) | b;
}

#if USE_CROP==1
#define GetImage GetImageCrop
#else
#define GetImage GetImageResize
#endif

int GetImageResize(camera_fb_t * fb, TfLiteTensor* input) 
{
    // MicroPrintf("fb: %dx%d-fmt:%d-len:%d INPUT: %dx%d", fb->width, fb->height, fb->format, fb->len, INPUT_W, INPUT_H);
    assert(fb->format == PIXFORMAT_RGB565);

    int x_scale = fb->width / INPUT_W;
    int y_scale = fb->height / INPUT_H;
    // MicroPrintf("x_scale=%d y_scale=%d\n", x_scale, y_scale);

    int post = 0;
    for (int y = 0; y < INPUT_H; y++) {
        for (int x = 0; x < INPUT_W; x++) {
            int getPos = y_scale * y * fb->width + x * x_scale;
            uint16_t color = ((uint16_t *)fb->buf)[getPos];
            uint32_t rgb = rgb565torgb888(color);
            uint8_t r = (rgb >> 16) & 0xFF; // rgb_image_data[getPos*3];
            uint8_t g = (rgb >>  8) & 0xFF; // rgb_image_data[getPos*3+1];
            uint8_t b = (rgb >>  0) & 0xFF; // rgb_image_data[getPos*3+2];
#if USE_INT8==1
            int8_t *image_data = input->data.int8;
            image_data[post * 3 + 0] = (int)r - 128;  // R
            image_data[post * 3 + 1] = (int)g - 128;  // G
            image_data[post * 3 + 2] = (int)b - 128;  // B
            // if (post < 3) printf("input[%d]: %d %d %d\n", post, image_data[post * 3 + 0] + 128, image_data[post * 3 + 1] + 128, image_data[post * 3 + 2] + 128);
#else
            float *image_data = input->data.f;
            image_data[post * 3 + 0] = (float) r / 255.0;
            image_data[post * 3 + 1] = (float) g / 255.0;
            image_data[post * 3 + 2] = (float) b / 255.0;
#endif /* USE_INT8*/
            post++;
        }
    }
    return 0;
}
int GetImageCrop(camera_fb_t * fb, TfLiteTensor* input) 
{
    // MicroPrintf("fb: %dx%d-fmt:%d-len:%d INPUT: %dx%d", fb->width, fb->height, fb->format, fb->len, INPUT_W, INPUT_H);
    assert(fb->format == PIXFORMAT_RGB565);

    // Trimming Image
    int post = 0;
    int startx = (fb->width - INPUT_W) * 0.50;
    int starty = (fb->height - INPUT_H) * 0.75;
    // printf("startx=%d starty=%d\n", startx, starty);

    for (int y = 0; y < INPUT_H; y++) {
        for (int x = 0; x < INPUT_W; x++) {
            int getPos = (starty + y) * fb->width + startx + x;
            // MicroPrintf("input[%d]: fb->buf[%d]=%d\n", post, getPos, fb->buf[getPos]);
            uint16_t color = ((uint16_t *)fb->buf)[getPos];
            uint32_t rgb = rgb565torgb888(color);
            uint8_t r = (rgb >> 16) & 0xFF; // rgb_image_data[getPos*3];
            uint8_t g = (rgb >>  8) & 0xFF; // rgb_image_data[getPos*3+1];
            uint8_t b = (rgb >>  0) & 0xFF; // rgb_image_data[getPos*3+2];
#if USE_INT8==1
            int8_t *image_data = input->data.int8;
            image_data[post * 3 + 0] = (int)r - 128;  // R
            image_data[post * 3 + 1] = (int)g - 128;  // G
            image_data[post * 3 + 2] = (int)b - 128;  // B
            // if (post < 3) printf("input[%d]: %d %d %d\n", post, image_data[post * 3 + 0] + 128, image_data[post * 3 + 1] + 128, image_data[post * 3 + 2] + 128);
#else
            float *image_data = input->data.f;
            image_data[post * 3 + 0] = (float) r / 255.0;
            image_data[post * 3 + 1] = (float) g / 255.0;
            image_data[post * 3 + 2] = (float) b / 255.0;
#endif /* USE_INT8*/
            post++;
        }
    }

    return 0;
}

inline float rad2deg(float rad) 
{
  return 180.0*rad/3.14;
}

// steering
#define CENTER 0
#define RIGHT 1
#define LEFT 2

int GetAction(float rad)
{
    int deg = (int)rad2deg(rad);
    if (deg < 10 and deg > -10)
        return CENTER;
    else if (deg >= 10)
        return RIGHT;
    else if (deg < -10)
        return LEFT;
    return -1;
}

void dnn_loop(camera_fb_t *fb)
{
  int64_t fr_begin, fr_cap, fr_pre, fr_dnn;
  static int64_t last_frame = 0;

  printf("Starting DNN loop on core %d\n", xPortGetCoreID());

  if (fb == NULL) {
    Serial.println("Camera capture failed");
    return;
  } else if (fb->format != PIXFORMAT_RGB565) {
    printf("%s: Camera capture has unsupported format %d\n", __FUNCTION__, fb->format);
    return;
  }

  fr_begin = esp_timer_get_time();

  if (!last_frame)
    last_frame = fr_begin;


  fr_cap = esp_timer_get_time();

#if DEBUG_TFLITE==0
  GetImage(fb, g_nn->getInput());
#else
  // Use a static image for debugging
  memcpy(g_nn->getInput()->data.int8, img_data, sizeof(img_data));
  printf("input: %d %d %d...\n", 
      g_nn->getInput()->data.int8[0], g_nn->getInput()->data.int8[1], g_nn->getInput()->data.int8[2]);
#endif
  fr_pre = esp_timer_get_time();

  if (kTfLiteOk != g_nn->predict())
  {
      printf("Invoke failed.\n");
  }
#if USE_INT8==1
  int q = g_nn->getOutput()->data.int8[0];
  float scale = g_nn->getOutput()->params.scale;
  int zero_point = g_nn->getOutput()->params.zero_point;
  float angle = (q - zero_point) * scale;
#else
  float angle = g_nn->getOutput()->data.f[0];
#endif
  fr_dnn = esp_timer_get_time();

  int deg = (int)rad2deg(angle);

  // set steering  
  set_steering(deg);
  printf("deg=%d (%.3f)\n", deg, angle);

  int64_t fr_end = esp_timer_get_time();
  int64_t frame_time = (fr_end - last_frame)/1000;

  printf("%s: Core%d: %s (prio=%d) %ums (%.1ffps): cap=%dms, pre=%dms, dnn=%dms\n",
      __FUNCTION__,
      xPortGetCoreID(), pcTaskGetName(NULL), uxTaskPriorityGet(NULL),
      (uint32_t)frame_time, 1000.0 / (uint32_t)frame_time,
      (int)((fr_cap-fr_begin)/1000), (int)((fr_pre-fr_cap)/1000), (int)((fr_dnn-fr_pre)/1000));

  last_frame = fr_end;  
}
